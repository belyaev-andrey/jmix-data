plugins {
    id "com.jfrog.bintray" version "1.7"
    id 'java-library'
    id 'maven-publish'
}

group = 'io.jmix.data'
version = '0.0.1-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

jar {
    baseName = project.getName()
    version =  project.getVersion()
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadocJar.dependsOn javadoc

publishing {
    publications {
        maven(MavenPublication) {
            groupId project.getGroup()
            artifactId project.getName()
            version project.getVersion()

            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

dependencies {
    compile "org.apache.commons:commons-lang3:3.9"
    compile "org.springframework:spring-core:5.1.8.RELEASE"
    compile "org.springframework:spring-beans:5.1.8.RELEASE"
    compile "org.springframework:spring-context-support:5.1.8.RELEASE"
    compile "org.springframework.data:spring-data-jpa:2.1.9.RELEASE"
    compile "javax.persistence:javax.persistence-api:2.2"

    annotationProcessor ("org.projectlombok:lombok-maven-plugin:1.18.6.0")

    testCompile "org.springframework:spring-test:5.1.8.RELEASE"
    testCompile "org.apache.commons:commons-lang3:3.8"
    testCompile "junit:junit:4.12"
    testCompile "org.jmockit:jmockit:1.43"
}

test {
    jvmArgs "-javaagent:${classpath.find { it.name.contains("jmockit") }.absolutePath}"
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    //use maven publication
    publications = ['maven']

    // make files public ?
    publish = true
    // override existing artifacts?
    override = false

    // metadata
    pkg {
        repo = 'labs'           // your repository name
        name = 'spring-script-repositories'  // package name - it will be created upon upload
        userOrg = 'cuba-platform'
        licenses = ["Apache-2.0"]
        websiteUrl = 'https://github.com/cuba-rnd/spring-script-repositories'
        issueTrackerUrl = 'https://github.com/cuba-rnd/spring-script-repositories/issues'
        vcsUrl = 'https://github.com/cuba-rnd/spring-script-repositories.git' // Mandatory for Open Source projects
        labels = ['spring', 'scripting', 'opensource']
        publicDownloadNumbers = true
    }
}
